.program vga_hsync

; frontporch: 16 clocks (0.64us at 25MHz)
; sync pulse: 96 clocks (3.84us at 25MHz)
; back porch: 48 clocks (1.92us at 25MHz)
; active for: 640 clcks (25.6us at 25MHz)
;
; High for 704 cycles (28.16us at 25MHz)
; Low  for 96  cycles (3.84us at 25MHz)
; Total period of 800 cycles (32us at 25MHz)

pull block              ; pull active timing value from fifo to osr
.wrap_target       

; ACTIVE + FRONTPORCH
mov x, osr          
activeporch:
   jmp x-- activeporch 

; SYNC PULSE
pulse:
    
    set pins, 0 [31]    ; hsync pulse   (32 cycles)
    set pins, 0 [31]    ;               (64 cycles)
    set pins, 0 [31]    ;               (96 cycles)
    

; BACKPORCH
backporch:
    set pins, 1 [31]    ; back porch    (32 cycles)
    set pins, 1 [12]    ;               (45 cycles)
    irq 0       [1]     ;               (47 cycles)
.wrap




% c-sdk {
static inline void vga_hsync_program_init(PIO pio, uint sm, uint offset, uint pin) {

    pio_sm_config c = vga_hsync_program_get_default_config(offset);

    sm_config_set_set_pins(&c, pin, 1);

    sm_config_set_clkdiv(&c, 5) ;

    pio_gpio_init(pio, pin);
    
    pio_sm_set_consecutive_pindirs(pio, sm, pin, 1, true);

    pio_sm_init(pio, sm, offset, &c);

}
%}